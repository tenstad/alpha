WHITESPACE   =  _{ " " | "\t" | NEWLINE }
  
program      =   { SOI ~ expr ~ EOI }
  expr       =   { prefix? ~ primary ~ postfix? ~ (infix ~ prefix? ~ primary ~ postfix? )* }
    infix    =  _{ add | sub | mul | div | pow }
      add    =   { "+" }
      sub    =   { "-" }
      mul    =   { "*" }
      div    =   { "/" }
      pow    =   { "^" }
    prefix   =  _{ neg }
      neg    =   { "-" }
    postfix  =  _{ fac }
      fac    =   { "!" }
    primary  =  _{ "(" ~ expr ~ ")" | list | int }
      list   =  { "[" ~ expr ~ ("," ~ expr )* ~ "]" }
      int    =  @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }
