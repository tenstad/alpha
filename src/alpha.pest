WHITESPACE   =  _{ " " | "\t" | NEWLINE }
  
program           =   { SOI ~ statements ~ EOI }
  statements      =   { (statement | semistatement)* }
    statement     =   { looop }
      looop       =   { "for" ~ name ~ "in" ~ expr ~ "{" ~ statements ~ "}" }
    semistatement =  _{ (var | fun | expr) ~ ";" }
      fun         =   { name ~ "(" ~ expr ~ ("," ~ expr)* ~ ")" }
      var         =   { def? ~ name ~ "=" ~ expr }
        def       =   { "let" ~ "mut"? }
        name      =  @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA | "_")* }
      expr        =   { prefix? ~ primary ~ postfix? ~ (infix ~ prefix? ~ primary ~ postfix? )* }
        infix     =  _{ add | sub | mul | div | pow }
          add     =   { "+" }
          sub     =   { "-" }
          mul     =   { "*" }
          div     =   { "/" }
          pow     =   { "^" }
        prefix    =  _{ neg }
          neg     =   { "-" }
        postfix   =  _{ fac }
          fac     =   { "!" }
        primary   =  _{ "(" ~ expr ~ ")" | varref | list | range | int }
          list    =   { "[" ~ expr ~ ("," ~ expr )* ~ "]" }
          range   =   { int ~ ".." ~ int }
          int     =  @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }
          varref  =  @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA | "_")* }
